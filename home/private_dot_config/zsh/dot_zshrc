# Copyright Cameron Tuckerman-Lee 2024. All rights reserved.

bindkey -v

# Autocomplete
autoload -Uz compinit
compinit -i  # -i skips insecure-dir warnings
autoload -Uz +X bashcompinit && bashcompinit

# History
HISTDIR=${XDG_STATE_HOME:-$HOME/.local/state}/zsh
mkdir -p "$HISTDIR"
HISTFILE="$HISTDIR/zsh_history"
HISTSIZE=200000
SAVEHIST=200000

setopt SHARE_HISTORY  # merge history across sessions
setopt INC_APPEND_HISTORY  # write to $HISTFILE immediately
setopt EXTENDED_HISTORY  # timestamps + durations
setopt HIST_IGNORE_DUPS  # drop consecutive dup
setopt HIST_FIND_NO_DUPS  # skip dup matches when searching
setopt HIST_EXPIRE_DUPS_FIRST  # expire dups first when trimming

autoload -Uz history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end  history-search-end

typeset -g -A key
key[Up]="${terminfo[kcuu1]}"
key[Down]="${terminfo[kcud1]}"

[[ -n $key[Up]   ]] && bindkey "$key[Up]"   history-beginning-search-backward-end
[[ -n $key[Down] ]] && bindkey "$key[Down]" history-beginning-search-forward-end

# rc.d
for file in $HOME/.config/zsh/rc.d/*; do
  source "$file"
done

# hooks (this should be last)
command -v rbenv &>/dev/null && eval "$(rbenv init - zsh)"
command -v pyenv &>/dev/null && eval "$(pyenv init -)"
command -v nodenv &>/dev/null && eval "$(nodenv init -)"
command -v direnv &>/dev/null && eval "$(direnv hook zsh)"
command -v starship &>/dev/null && eval "$(starship init zsh)"
